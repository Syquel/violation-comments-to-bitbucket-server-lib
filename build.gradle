buildscript {
 repositories {
  jcenter()
  maven {
   url "https://plugins.gradle.org/m2/"
  }
 }
 dependencies {
  classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  classpath 'net.researchgate:gradle-release:2.2.2'
  classpath "gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.36"
 }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'net.researchgate.release'
apply plugin: "se.bjurr.gitchangelog.git-changelog-gradle-plugin"

task gitChangelogTask(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
 filePath = "CHANGELOG.md";
 templateContent = """
# Changelog
{{#tags}}
## {{name}}
 {{#issues}}
  {{#hasIssue}}
   {{#hasLink}}
### {{name}} [{{issue}}]({{link}}) {{title}}
   {{/hasLink}}
   {{^hasLink}}
### {{name}} {{issue}} {{title}}
   {{/hasLink}}
  {{/hasIssue}}
  {{^hasIssue}}
### {{name}}
  {{/hasIssue}}

  {{#commits}}
**{{{messageTitle}}}**

{{#messageBodyItems}}
 * {{.}} 
{{/messageBodyItems}}

[{{hash}}](https://github.com/tomasbjerre/violation-comments-to-bitbucket-server-lib/commit/{{hash}}) {{authorName}} *{{commitTime}}*

  {{/commits}}

 {{/issues}}
{{/tags}}
 """;
 removeIssueFromMessage = true
}

group = 'se.bjurr.violations'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
 jcenter()
 mavenLocal()
 mavenCentral()
}

dependencies {
 compile 'se.bjurr.violations:violation-comments-lib:1.40'
 compile 'com.jayway.jsonpath:json-path:2.0.0'
 compile 'com.google.code.gson:gson:2.8.0'
 compile 'com.google.guava:guava:20.0'

 testCompile 'junit:junit:4.12'
 testCompile 'org.assertj:assertj-core:2.3.0'
 testCompile ('org.assertj:assertj-core:2.5.0')
}

eclipse {
 classpath {
  downloadSources = true
  downloadJavadoc = true
 }
}

sourceSets {
 main.java.srcDirs = ['src/main/java' ]
 test.java.srcDirs = ['src/test/java' ]
}

if (JavaVersion.current().isJava8Compatible()) {
 allprojects {
  tasks.withType(Javadoc) {
   options.addStringOption('Xdoclint:none', '-quiet')
  }
 }
}

modifyPom {
 project {
  name 'Violation Comments to Bitbucket Server Lib'
  description 'Library that adds violation comments from static code analysis to Bitbucket Server.'
  url 'https://github.com/tomasbjerre/violation-comments-to-bitbucket-server-lib'
  inceptionYear '2016'
   scm {
   url 'https://github.com/tomasbjerre/violation-comments-to-bitbucket-server-lib'
   connection 'scm:https://tomasbjerre@github.com/tomasbjerre/violation-comments-to-bitbucket-server-lib.git'
   developerConnection 'scm:git://github.com/tomasbjerre/violation-comments-to-bitbucket-server-lib.git'
  }

  licenses {
   license {
    name 'The Apache Software License, Version 2.0'
    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    distribution 'repo'
   }
  }

  developers {
   developer {
    id 'tomasbjerre'
    name 'Tomas Bjerre'
    email 'tomas.bjerre85@gmail.com'
   }
  }
 }
}

extraArchive {
 sources = true
 tests = true
 javadoc = true
}

afterReleaseBuild.dependsOn {
 [install, uploadArchives]
}
